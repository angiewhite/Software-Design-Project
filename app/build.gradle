apply plugin: 'com.android.application'

apply plugin: "androidx.navigation.safeargs"

//apply plugin: 'kotlin-android'

//apply plugin: 'kotlin-android-extensions'

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        String result = stdout.toString().trim()
        if (result.indexOf('-') == -1) return result + ".0"
        return result.substring(0, result.indexOf('-')) + "." +
                result.substring(result.indexOf('-') + 1, result.lastIndexOf('-'))
    }
    catch (ignored) {
        return null
    }
}

def getVersionCode = { ->
    String versionName = getVersionName()
    if (versionName == null) return -1;
    Integer major = versionName.substring(0, versionName.indexOf('.')).toInteger()
    Integer minor = versionName.substring(versionName.indexOf('.') + 1, versionName.lastIndexOf('.')).toInteger()
    Integer patch = versionName.substring(versionName.lastIndexOf('.') + 1).toInteger()
    return 10000 * major + 1000 * minor + 100 * patch
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "bsuir.ksis.angieapp"
        minSdkVersion 22
        targetSdkVersion 28
        versionName getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            signingConfig signingConfigs.config
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all {
                    def apkName = "${applicationId}-${versionName}" +
                            ".apk"
                    outputFileName = apkName
                }
            }
        }
    }
    flavorDimensions "version"
    productFlavors {
        development {
            manifestPlaceholders = [orientationPreference: "unspecified"]
            versionNameSuffix = "-dev"
        }
        primary {
            manifestPlaceholders = [orientationPreference: "portrait"]
            versionNameSuffix = ""
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation files('libs/jbcrypt-0.3m.jar')
    //implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    implementation 'com.google.android.material:material:1.0.0'

    def nav_version = "1.0.0-alpha08"

    implementation "android.arch.navigation:navigation-fragment:$nav_version"
    implementation "android.arch.navigation:navigation-ui:$nav_version"

    def room_version = "2.1.0-alpha02"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1-beta01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1-beta01'
    implementation 'androidx.annotation:annotation:1.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha01'
}


task printVersion() << {
    println("Version Name: $getVersionName")
    println("Version Code: $getVersionCode")
}

//adb -s 254a48a49a0d7ece shell am start -W -a android.intent.action.VIEW -d "sdapp://bsuir.ksis.angieapp/page/4" bsuir.ksis.angieapp
