apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        String result = stdout.toString().trim()
        if (result.indexOf('-') == -1) return result + ".0"
        return result.substring(0, result.indexOf('-')) + "." +
                result.substring(result.indexOf('-') + 1, result.lastIndexOf('-'))
    }
    catch (ignored) {
        return null
    }
}

def getVersionCode = { ->
    String versionName = getVersionName()
    if (versionName == null) return -1;
    Integer major = versionName.substring(0, versionName.indexOf('.')).toInteger()
    Integer minor = versionName.substring(versionName.indexOf('.') + 1, versionName.lastIndexOf('.')).toInteger()
    Integer patch = versionName.substring(versionName.lastIndexOf('.') + 1).toInteger()
    return 10000 * major + 1000 * minor + 100 * patch
}

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "bsuir.ksis.angieapp"
        minSdkVersion 22
        targetSdkVersion 23
        versionCode getVersionCode()
        versionName getVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all {
                    def versionNameSuffix = variant.productFlavors[0].versionNameSuffix
                    def apkName = "${defaultConfig.applicationId}-${defaultConfig.versionName}" +
                            "${versionNameSuffix}.apk"
                    outputFileName = apkName
                }
            }
        }
    }

    flavorDimensions "version"
    productFlavors {
        development {
            manifestPlaceholders = [orientationPreference: "unspecified"]
            versionNameSuffix = "-dev"
        }
        primary {
            manifestPlaceholders = [orientationPreference: "portrait"]
            versionNameSuffix = ""
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:26.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation 'com.android.support:support-v4:26.0.0'
    implementation 'com.android.support:design:26.0.0'
}


task printVersion() << {
    println("Version Name: $getVersionName")
    println("Version Code: $getVersionCode")
}